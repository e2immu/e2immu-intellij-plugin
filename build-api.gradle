/*
 * e2immu: a static code analyser for effective and eventual immutability
 * Copyright 2020-2021, Bart Naudts, https://www.e2immu.org
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/e2immu/e2immu-support")
    }
}

dependencies {
    implementation group: 'org.e2immu', name: 'analyser-cli', version: '0.1.2'      // LGPL 3.0
    implementation group: 'org.e2immu', name: 'e2immu-support', version: '0.1.2'    // LGPL 3.0

    implementation group: 'io.vertx', name: 'vertx-web', version: '4.0.3'
    implementation group: 'io.vertx', name: 'vertx-config', version: '4.0.3'
}

/*
 ./gradlew -b build-api.gradle runIoVertxInspected
 */
task runIoVertxInspected(type: JavaExec) {
    group = "Execution"
    description = "Prepare an AnnotatedAPI file for io.vertx.core, based on inspection"

    classpath = sourceSets.main.runtimeClasspath
    main = 'org.e2immu.analyser.cli.Main'

    Set<File> reducedClassPath = sourceSets.main.runtimeClasspath.toList()
    reducedClassPath += sourceSets.test.runtimeClasspath
    reducedClassPath.removeIf({ f -> f.path.contains("build/classes") || f.path.contains("build/resources") })

    args('--classpath=' + reducedClassPath.join(":") + ":jmods/java.base.jmod:jmods/java.logging.jmod",
            '--write-annotated-api=INSPECTED',
            "--write-annotated-api-packages=io.vertx.core",
            "--source=none",
            "-d=CONFIGURATION,BYTECODE_INSPECTOR")
}

/*
 ./gradlew -b build-api.gradle runIoVertxUsage
 */
task runIoVertxUsage(type: JavaExec) {
    group = "Execution"
    description = "Prepare an AnnotatedAPI file for io.vertx.core"

    classpath = sourceSets.main.runtimeClasspath
    main = 'org.e2immu.analyser.cli.Main'

    Set<File> reducedClassPath = sourceSets.main.runtimeClasspath.toList()
    reducedClassPath += sourceSets.test.runtimeClasspath
    reducedClassPath.removeIf({ f -> f.path.contains("build/classes") || f.path.contains("build/resources") })

    args('--classpath=' + reducedClassPath.join(":") + ":jmods/java.base.jmod:jmods/java.logging.jmod",
            '--write-annotated-api=USAGE',
            '--write-annotated-api-destination-package=org.e2immu.kvstoreaapi',
            "--write-annotated-api-packages=io.vertx.,java.util.logging,java.time.",
            "--source=src/main/java",
            "--source-packages=org.e2immu.kvstore",
            "-d=CONFIGURATION,BYTECODE_INSPECTOR,ANNOTATED_API_WRITER")
}
